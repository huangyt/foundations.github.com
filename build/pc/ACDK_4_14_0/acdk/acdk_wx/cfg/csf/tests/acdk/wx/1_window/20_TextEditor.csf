
using acdk.wx;

// IDs for the controls and the menu commands
enum MyEvents
{
  // menu items
  Event_Quit = 1,
  Event_About,
  Event_FileNew,
  Event_FileOpen,
  Event_FileSave,
  Id_TextControl
}

class TextEditFrame
extends Frame
{
  TextCtrl editControl;
  TextEditFrame() 
  {
    editControl = new TextCtrl(this, Id_TextControl, "Simple Text Editor",
                               Point.defaultPosition(), Size.defaultSize(), TeMultiline | TeAutoScroll | TeDontwrap);
    MenuBar menuBar = new MenuBar();
    
    Menu menuFile = new Menu();
    menuFile.append(Event_FileNew, "New", "Creates a new file"); 
    menuFile.append(Event_FileOpen, "Open", "Open a file"); 
    menuFile.append(Event_FileSave, "Save", "Open a file"); 
    menuFile.appendSeparator();
    menuFile.append(Event_Quit, "E&xit\tAlt-X", "Quit this program");
    
    menuBar.append(menuFile, "&File");
    
    Menu helpMenu = new Menu();
    helpMenu.append(Event_About, "&About...\tCtrl-A", "Show about dialog");
    menuBar.append(helpMenu, "&Help");
    setMenuBar(menuBar);
    
    connect(CommandEvent.EvtCommandMenuSelected, Event_FileNew, delegate onNew);
    connect(CommandEvent.EvtCommandMenuSelected, Event_FileOpen, delegate onOpen);
    connect(CommandEvent.EvtCommandMenuSelected, Event_FileSave, delegate onSave);
    connect(CommandEvent.EvtCommandMenuSelected, Event_Quit, delegate onQuit);
    connect(CommandEvent.EvtCommandMenuSelected, Event_About, delegate onAbout);
    
    setTitle("Test");
  }
  void onNew(Event event)
  {
    editControl.setValue("");
    editControl.discardEdits();
  }
  void onOpen(Event event)
  {
    Dialog dlg = new FileDialog(this, message: "Open a file", 
                                      wildCard: "*.txt;*.csf",
                                      defaultDir: "",
                                      defaultFile: "",
                                      style: FDFOpen);
    if (dlg.showModal() != IdOk)
      return;
    if (true) // acdk way
    {
      editControl.setValue((new acdk.io.File(dlg.getPath())).getReader().getCharReader().readString());
    } 
    else
    {
      editControl.loadFile(dlg.getPath());
    }
  }
  void onSave(Event event)
  {
    Dialog dlg = new FileDialog(this, "Save a file", "", "", "*.txt;*.csf", FDFSave);
    if (dlg.showModal() != IdOk)
      return;
    String fname = dlg.getPath();
    if (true) // acdk way
    { 
      (new acdk.io.File(fname)).getWriter().getCharWriter().writeString(editControl.getValue());
    }
    else
    {
      editControl.saveFile(fname);
    }
  }
  void onQuit(Event event)
  {
    if (editControl.isModified() == true)
    {
       if (Window.messageBox("File unsaved. Really want to quit?", "Text Editor", MbYesNo, this) == MbNo)
        return;
    }
    close(true);
  }
  void onAbout(CommandEvent event)
  {
    Window.messageBox("Simple Text editor written with ACDK CfgScript\n By Roger Rene Kommer (http://acdk.sourceforge.net)");
  }
}

class MyApp
extends acdk.wx.App
{
  MyApp() {}
  bool onInit()
  {
    TextEditFrame win = new TextEditFrame();
    win.show(true);
    return true;
  }
}

StringArray args = new StringArray(0);
acdk.wx.App.createGui("MyApp", args); // run the application until MyFrame is closed

